plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    subprojects.each {
        include project("${it.name}:")
    }
}

allprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    ext {
        post_seventeen = false
    }

    tasks.withType(JavaCompile).configureEach {
        if (post_seventeen) {
            it.options.release.set(16)
        }
        else {
            it.options.release.set(8)
        }
    }

    // Including this because without it I can't actually use mixins for some reason???
    dependencies {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    }

    processResources {
        inputs.property "version", rootProject.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": rootProject.version
        }
    }

    /*task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }*/
}

subprojects {
    version rootProject.version

    dependencies {
        implementation rootProject
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}
